import java.util.regex.Pattern

apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    lintOptions {
        checkReleaseBuilds false
    }

    def manifestFile = file("src/main/AndroidManifest.xml")
    def pattern = Pattern.compile("versionCode=\"(\\d+)\"")
    def manifestText = manifestFile.getText()
    def matcher = pattern.matcher(manifestText)
    matcher.find()
    def code = Integer.parseInt(matcher.group(1)) + 1
    def manifestContent = matcher.replaceAll("versionCode=\"" + code + "\"")

    def date = new Date()
    def formattedDate = date.format('ddMMyy-HHmm')

    def pattern2 = Pattern.compile("versionName=\"(.*?[_]*\\d+\\.\\d+\\.\\d+\\.)\\d+[_]\\d{6}\\-\\d{4}\"")
    def matcher2 = pattern2.matcher(manifestContent)
    matcher2.find()
    def prefix = matcher2.group(1)
    def newVersionName = prefix + code + "_" + formattedDate

    def completeString = matcher2.replaceAll("versionName=\"" + newVersionName + "\"")

    manifestFile.write(completeString)


    defaultConfig {
        applicationId "com.maksl5.bl_hunt"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode(code)
        versionName newVersionName
    }

    signingConfigs {
        release {
            storeFile file("bluehunter.keystore")
            storePassword System.getenv("STOREPWD")
            keyAlias "maksl5"
            keyPassword System.getenv("KEYPWD")
        }
    }


    productFlavors {
        free {
            applicationId "com.maksl5.bl_hunt"
            buildConfigField "boolean", "IS_SUPPORT", "false"
            versionCode code
            setVersionName newVersionName
        }
        support {
            applicationId "com.maksl5.bl_hunt_sup"
            buildConfigField "boolean", "IS_SUPPORT", "true"
            versionCode code
            versionName "support_" + newVersionName
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }


}


dependencies {
    compile 'com.android.support:support-v4:23.1.1'
    compile 'joda-time:joda-time:2.9'
    compile 'com.google.android.gms:play-services-ads:8.3.0'
    compile 'ch.acra:acra:4.7.0'
}
